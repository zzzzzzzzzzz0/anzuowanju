模块“公用”。

定义“提示”以下代码
	弄个‘参数0’、‘参数栈’。
上代码。
定义“吐泡”以下代码
	弄个‘参数0’、‘参数栈’。
上代码。
定义“推条”以下代码
	弄个‘参数0’、‘参数栈’。
上代码。
定义“刷通知”以下代码
	弄个‘参数0’、‘参数栈’。
上代码。

定义“正匹配”【倒挂】、“串”、“表达式”以下代码
	弄个“re”、“1”、‘串’、‘表达式’。
上代码。
定义“如”、“串”、“正匹”【隔】、“式”、“那”【隔】、“代码”以下代码
	弄个“re”、、‘串’、‘式’、‘代码’。
上代码。
定义“++”、“后”以下代码
	赋予‘后’【上】以算术“‘‘后’’+1”。
上代码。
定义“得参数”、“号”、“值”【隔】以下代码
	‘参数‘号’【上】’
上代码。
定义“得”、“名”、“值”【隔】以下代码
	‘‘名’’
上代码。

定义“直接发短信”、“内容”、“给”【隔】、“号码”以下代码
	弄个“sms”、‘号码’、‘内容’、‘参数栈3’。
上代码。
定义“发短信”、“内容”、“给”【隔】、“号码”以下代码
	弄个“start”、“sendto”、“smsto:‘号码’”、“sms_body”、‘内容’。
上代码。
定义“打电话给”、“号码”以下代码
	弄个“start”、“call”、“tel:‘号码’”。
上代码。
定义“发邮件给”、“邮箱”、“主题”【隔】、“主题”、“内容”【隔】、“内容”以下代码
	弄个“start”、“sendto”、“mailto:‘邮箱’”、
		“.subject”、‘主题’、
		“.text”、‘内容’。
上代码。

定义“加标签”、“标签”、“属性”、“内容”以下代码
	“<‘标签’ ‘属性’>‘内容’</‘标签’>”。
上代码。

赋予“1链”【私】以“1”。
定义“加链”【不定】、“地址”、“说明”以下代码
	（显示‘参数0’--‘参数’。）
	赋予“它前”、“它后”、“它型”、“头”、“它”以弄个“参”、“4”、
		“-它前”、“-它后”、“-它型”、“-头”、‘参数栈3’。
	如果‘它前’等于“NULL”那么赋予“它前”以。
	如果‘它后’等于“NULL”那么赋予“它后”以。
	如果‘1链【公用】’那么
		赋予“1链”【公用】以“0”
	否则
		如果‘头’那么‘头’否则“<br><br>”。
	如果不‘说明’那么
		赋予“说明”以‘地址’。
	赋予“地址”以弄个“url”、‘地址’。
	分支‘它型’先
		“码”先
			赋予“代码”、“它”以弄个“参”、“1”、、解释‘它’。
			赋予“它”以弄个“换码”、“解释下原样‘代码’上原样、下原样‘说明’上原样”
				如果‘它’不等于“”那么“、‘它’”。
		了。
		赋予“它”以解释‘它’。
	了。
	（显示‘地址’换行‘它前’‘它’‘它后’换行‘说明’。）
	加标签“a”、“href="‘地址’" style="white-space:nowrap;" ‘它前’‘它’‘它后’”、‘说明’。
上代码。
赋予“横格”以“ &nbsp; &nbsp; ”。
定义“横加链”【不定】、“地址”、“说明”以下代码
	加链‘地址’、‘说明’、“-头”、‘横格’、‘参数栈3’。
上代码。

定义“加爽链”【不定】、“地址”以下代码
	加链“‘地址’##adblock”、‘参数栈2’。
上代码。
定义“横加爽链”【不定】、“地址”、“说明”以下代码
	加爽链‘地址’、‘说明’、“-头”、‘横格’、‘参数栈3’。
上代码。

定义“加新开链”【不定】、“地址”、“说明”以下代码
	加链‘地址’、‘说明’、“target=_blank”、‘参数栈3’。
上代码。
定义“横加新开链”【不定】、“地址”、“说明”以下代码
	加新开链‘地址’、‘说明’、“-头”、‘横格’、‘参数栈3’。
上代码。

定义“加钮”【不定】、“代码”、“说明”以下代码
	如果不‘说明’那么
		赋予“说明”以‘代码’。
	赋予“js”、“它”以弄个“参”、“1”、“-js-”、‘参数栈3’。
	加链“javascript:”、‘说明’、
		“-它前”、“onclick="” 先如果不‘js’那么“z$.f1('”了、
		“-它后”、先如果不‘js’那么“')”了 “;return false"”、
		“-它型”、先如果不‘js’那么“码”了、
		‘代码’、解释‘它’。
上代码。
定义“横加钮”【不定】、“代码”、“说明”以下代码
	加钮‘代码’、‘说明’、“-头”、‘横格’、‘参数栈3’。
上代码。

定义“初始化链”以下代码
	赋予“1链”【公用】以“1”。
上代码。

定义“加文”以下代码
  赋予“属性”以。
  赋予“大小”、“参”以弄个“参”、“1”、“-大小”、‘参数栈’。
  如果‘大小’那么赋予“属性”以‘属性’“font-size:‘大小’px;”。
  赋予“颜色”、“参”以弄个“参”、“1”、“-颜色”、解释‘参’。
  如果‘颜色’那么赋予“属性”以‘属性’“color:‘颜色’;”。
  赋予“内容”以解释‘参’。
  加标签“span”、先如果‘属性’那么“style="‘属性’"”了、‘内容’。
上代码。

定义“视情况关闭”以下代码
	如果弄个“他打开”那么弄个“退按时”、“结束”。
上代码。

定义“自编辑”以下代码
  加钮“弄个“编辑”、‘参数0【上】’、“-改后”为“弄个刷新””、如果‘参数数目’那么‘参数1’否则‘参数0’。
上代码。

定义“查变量区”、“遮”、“层”、“代码”以下代码
	调用“/$forqv/-$this-$qv-$qv-c*-c*-i-R”、先如果存在“代码”那么‘代码’否则下代码
		显示‘参数’换行。
	上代码了、先如果‘遮’那么‘遮’否则“vV”（qaVvdfm 0为不i）了、先如果‘层’那么‘层’否则-1了。
上代码。

定义“唤zxing”【不定】、“模式”、“代码”以下代码
	弄个“start”、
	“-补代码”为下代码
		吐泡‘参数1’。
		弄个“菜单”、、下代码
			如果‘参数1’等于“是”那么
				弄个“打开”、“market://details?id=com.google.zxing.client.android”。
		上代码、“-标题”为“到商店安装“条码扫描器” app？”、“是”、“否”。
	上代码、
	“com.google.zxing.client.android.SCAN”、
	“-包名”、“com.google.zxing.client.android”、
	“-flags”、“CLEAR_TOP”（0x04000000）、
	“SCAN_MODE”、‘模式’、
	“----”、
	‘代码’、“0x0000c0de”、“-1”、
	先
	如果‘参数数目’大于“2”那么‘参数栈3’否则解释下代码
		“SCAN_RESULT”、
		“SCAN_RESULT_FORMAT”、
		“SCAN_RESULT_ORIENTATION:i”、
		“SCAN_RESULT_ERROR_CORRECTION_LEVEL”
	上代码
	了。
上代码。
定义“发声”【不定】、“文”以下代码
	弄个“tts”、‘文’、如果‘参数数目’大于“1”那么‘参数栈2’否则解释下代码
		“zh”、“-优包名”、“com.iflytek.(tts|vflynote)”
	上代码。
上代码。

赋予“软件作者”以“13530617167”。
